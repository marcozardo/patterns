model *Kimmel2021_CARTcellTherapy()

  /*
  This model describes the dynamics and interactions among normal T cells (N),
  chimeric antigen receptor (CAR) T cells (C), and tumour cells (B) following
  CAR T cell therapy. The model is based on a system of ordinary differential
  equations proposed by Kimmel et al. (2021). It captures the competition
  between T cell populations using Gompertz growth laws and models tumour
  killing by CAR T cells with a saturating Michaelis-Menten-like term.

  The model was parameterized using clinical data from the ZUMA-1 trial.
  */

  //// Compartments and Species:
  compartment patient;
  species $N in patient, $C in patient, $B in patient;

  //// Assignment Rules:
  T := N + C;
  r_C := rho_C + (b*(T - K_N)^2)/(a*T^2 + (T - K_N)^2);

  //// Rate Rules:
  N' = -r_N * N * log((N + C)/K_N);
  C' = -r_C * C * log((N + C)/K_C);
  B' = r_B * B - gamma_B * B * C/(k_B + C);

  //// Species initializations:
  N = 3.00e9;   // Initial median normal T cell number
  C = 1.80e8;   // Initial CAR T cell number
  B = 9.486e10; // Initial median tumour cell number

  //// Compartment initializations:
  patient = 1;

  //// Variable initializations:
  K_N = 2.50e11;
  K_C = 6.96e10;
  r_N = 0.170;
  rho_C = 0.0251;
  a = 0.423;
  b = 0.525;
  r_B = 0.19;      # Representative medium tumour growth rate from simulations in Fig 3b.
  gamma_B = 1.15;
  k_B = 2.024e9;

  //// Other declarations:
  var T, r_C;
  const patient, K_N, K_C, r_N, rho_C, a, b, r_B, gamma_B, k_B;

  //// Unit definitions:
  unit day = 86400 second;
  unit cells = 1 item;

  //// Display Names:
  day is "day";
  cells is "cells";
  patient is "Patient";
  N is "Normal T cells";
  C is "CAR T cells";
  B is "Tumour cells";
  T is "Total lymphocyte count";
  r_C is "CAR T cell net growth rate function";
  K_N is "Normal T cell carrying capacity";
  K_C is "CAR T cell carrying capacity";
  r_N is "Normal T cell net growth rate";
  rho_C is "Baseline CAR T net growth rate";
  a is "Signalling inefficiency factor in r_C";
  b is "Immune reconstitution impact in r_C";
  r_B is "Tumour net growth rate";
  gamma_B is "Tumour-killing rate by effector CAR";
  k_B is "Killing rate saturation parameter";
end
