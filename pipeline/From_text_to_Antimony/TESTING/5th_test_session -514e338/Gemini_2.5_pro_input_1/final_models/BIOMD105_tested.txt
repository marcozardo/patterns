model *Proctor2007_UbiquitinProteasomeSystem()

  //// Compartments and Species:
  compartment cell;
  species NatP in cell, MisP in cell, Ub in cell, E1 in cell, E2 in cell, E3 in cell;
  species DUB in cell, Proteasome in cell, $ROS in cell, $ATP in cell, $ADP in cell, $AMP in cell;
  species E3_MisP in cell, E1_Ub in cell, E2_Ub in cell, MisP_Ub in cell;
  species MisP_Ub2 in cell, MisP_Ub3 in cell, MisP_Ub4 in cell, MisP_Ub5 in cell;
  species MisP_Ub6 in cell, MisP_Ub7 in cell, MisP_Ub8 in cell;
  species MisP_Ub4_Proteasome in cell, MisP_Ub5_Proteasome in cell, MisP_Ub6_Proteasome in cell;
  species MisP_Ub7_Proteasome in cell, MisP_Ub8_Proteasome in cell;
  species AggP in cell, SeqAggP in cell, AggP_Proteasome in cell;

  //// Reactions:
  # Protein synthesis, misfolding and refolding
  J1:            => NatP;                           k1;
  J2: NatP + ROS => MisP + ROS;                     k2*NatP*ROS;
  J3: MisP       => NatP;                           k3*MisP;

  # Binding of E3 to misfolded protein (reversible)
  J4: E3 + MisP => E3_MisP;                         k61*E3*MisP;
  J5: E3_MisP   => E3 + MisP;                         k61r*E3_MisP;

  # Action of ubiquitin enzymes, E1 and E2 leading to first ubiquitination event
  J6: E1 + Ub + ATP => E1_Ub + AMP;                  k62*E1*Ub*ATP/(5000 + ATP);
  J7: E2 + E1_Ub    => E1 + E2_Ub;                    k63*E2*E1_Ub;
  J8: E2_Ub + E3_MisP => E2 + E3 + MisP_Ub;          k64*E2_Ub*E3_MisP;

  # Extending the ubiquitin chain
  J9:  E2_Ub + MisP_Ub  => MisP_Ub2 + E2;             k65*E2_Ub*MisP_Ub;
  J10: E2_Ub + MisP_Ub2 => MisP_Ub3 + E2;             k65*E2_Ub*MisP_Ub2;
  J11: E2_Ub + MisP_Ub3 => MisP_Ub4 + E2;             k65*E2_Ub*MisP_Ub3;
  J12: E2_Ub + MisP_Ub4 => MisP_Ub5 + E2;             k65*E2_Ub*MisP_Ub4;
  J13: E2_Ub + MisP_Ub5 => MisP_Ub6 + E2;             k65*E2_Ub*MisP_Ub5;
  J14: E2_Ub + MisP_Ub6 => MisP_Ub7 + E2;             k65*E2_Ub*MisP_Ub6;
  J15: E2_Ub + MisP_Ub7 => MisP_Ub8 + E2;             k65*E2_Ub*MisP_Ub7;

  # De-ubiquitination of free chains by DUB
  J16: DUB + MisP_Ub8 => DUB + MisP_Ub7 + Ub;         k66*DUB*MisP_Ub8;
  J17: DUB + MisP_Ub7 => DUB + MisP_Ub6 + Ub;         k66*DUB*MisP_Ub7;
  J18: DUB + MisP_Ub6 => DUB + MisP_Ub5 + Ub;         k66*DUB*MisP_Ub6;
  J19: DUB + MisP_Ub5 => DUB + MisP_Ub4 + Ub;         k66*DUB*MisP_Ub5;
  J20: DUB + MisP_Ub4 => DUB + MisP_Ub3 + Ub;         k66*DUB*MisP_Ub4;
  J21: DUB + MisP_Ub3 => DUB + MisP_Ub2 + Ub;         k66*DUB*MisP_Ub3;
  J22: DUB + MisP_Ub2 => DUB + MisP_Ub + Ub;          k66*DUB*MisP_Ub2;
  J23: DUB + MisP_Ub  => DUB + MisP + Ub;             k66*DUB*MisP_Ub;

  # Binding to the proteasome
  J24: MisP_Ub4 + Proteasome => MisP_Ub4_Proteasome;  k67*MisP_Ub4*Proteasome;
  J25: MisP_Ub5 + Proteasome => MisP_Ub5_Proteasome;  k67*MisP_Ub5*Proteasome;
  J26: MisP_Ub6 + Proteasome => MisP_Ub6_Proteasome;  k67*MisP_Ub6*Proteasome;
  J27: MisP_Ub7 + Proteasome => MisP_Ub7_Proteasome;  k67*MisP_Ub7*Proteasome;
  J28: MisP_Ub8 + Proteasome => MisP_Ub8_Proteasome;  k67*MisP_Ub8*Proteasome;

  # De-ubiquitination of bound chains by DUB
  J29: DUB + MisP_Ub8_Proteasome => DUB + Ub + MisP_Ub7_Proteasome;  k68*DUB*MisP_Ub8_Proteasome;
  J30: DUB + MisP_Ub7_Proteasome => DUB + Ub + MisP_Ub6_Proteasome;  k68*DUB*MisP_Ub7_Proteasome;
  J31: DUB + MisP_Ub6_Proteasome => DUB + Ub + MisP_Ub5_Proteasome;  k68*DUB*MisP_Ub6_Proteasome;
  J32: DUB + MisP_Ub5_Proteasome => DUB + Ub + MisP_Ub4_Proteasome;  k68*DUB*MisP_Ub5_Proteasome;
  J33: DUB + MisP_Ub4_Proteasome => DUB + Ub + Proteasome + MisP_Ub3; k68*DUB*MisP_Ub4_Proteasome;

  # Proteasomal degradation
  J34: MisP_Ub4_Proteasome + ATP => ADP + Proteasome + 4 Ub; k69*MisP_Ub4_Proteasome*ATP/(5000 + ATP);
  J35: MisP_Ub5_Proteasome + ATP => ADP + Proteasome + 5 Ub; k69*MisP_Ub5_Proteasome*ATP/(5000 + ATP);
  J36: MisP_Ub6_Proteasome + ATP => ADP + Proteasome + 6 Ub; k69*MisP_Ub6_Proteasome*ATP/(5000 + ATP);
  J37: MisP_Ub7_Proteasome + ATP => ADP + Proteasome + 7 Ub; k69*MisP_Ub7_Proteasome*ATP/(5000 + ATP);
  J38: MisP_Ub8_Proteasome + ATP => ADP + Proteasome + 8 Ub; k69*MisP_Ub8_Proteasome*ATP/(5000 + ATP);

  # Protein aggregation
  J39: 2 MisP => AggP;                       k71*MisP*(MisP - 1)/2;
  J40: MisP + AggP => 2 AggP;                k71*MisP*AggP;
  J41: 2 MisP_Ub => AggP;                    k72*MisP_Ub*(MisP_Ub - 1)/2;
  J42: 2 MisP_Ub2 => AggP;                   k72*MisP_Ub2*(MisP_Ub2 - 1)/2;
  J43: 2 MisP_Ub3 => AggP;                   k72*MisP_Ub3*(MisP_Ub3 - 1)/2;
  J44: 2 MisP_Ub4 => AggP;                   k72*MisP_Ub4*(MisP_Ub4 - 1)/2;
  J45: 2 MisP_Ub5 => AggP;                   k72*MisP_Ub5*(MisP_Ub5 - 1)/2;
  J46: 2 MisP_Ub6 => AggP;                   k72*MisP_Ub6*(MisP_Ub6 - 1)/2;
  J47: 2 MisP_Ub7 => AggP;                   k72*MisP_Ub7*(MisP_Ub7 - 1)/2;
  J48: 2 MisP_Ub8 => AggP;                   k72*MisP_Ub8*(MisP_Ub8 - 1)/2;
  J49: MisP + MisP_Ub => AggP;               k72*MisP*MisP_Ub;
  J50: MisP_Ub7 + MisP_Ub8 => AggP;          k72*MisP_Ub7*MisP_Ub8;

  # Sequestering of aggregates and proteasome inhibition
  J51: AggP => SeqAggP;                      k73*AggP;
  J52: AggP + Proteasome => AggP_Proteasome; k74*AggP*Proteasome;


  //// Events:
  E1_shutdown: at (time >= 1800): k62 = 0;


  //// Species initializations:
  NatP = 500;
  MisP = 0;
  Ub = 500;
  E1 = 100;
  E2 = 100;
  E3 = 100;
  DUB = 200;
  Proteasome = 100;
  ROS = 10;
  ATP = 10000;
  ADP = 1000;
  AMP = 1000;
  E3_MisP = 0;
  E1_Ub = 0;
  E2_Ub = 0;
  MisP_Ub = 0;
  MisP_Ub2 = 0;
  MisP_Ub3 = 0;
  MisP_Ub4 = 0;
  MisP_Ub5 = 0;
  MisP_Ub6 = 0;
  MisP_Ub7 = 0;
  MisP_Ub8 = 0;
  MisP_Ub4_Proteasome = 0;
  MisP_Ub5_Proteasome = 0;
  MisP_Ub6_Proteasome = 0;
  MisP_Ub7_Proteasome = 0;
  MisP_Ub8_Proteasome = 0;
  AggP = 0;
  SeqAggP = 0;
  AggP_Proteasome = 0;

  //// Compartment initializations:
  cell = 1;

  //// Variable initializations:
  k1 = 0.01;      # Protein synthesis
  k2 = 2.0E-6;    # Misfolding
  k3 = 4.0E-6;    # Refolding
  k61 = 1.7E-5;   # MisP/E3 binding
  k61r = 2.0E-4;  # MisP/E3 release
  k62 = 2.0E-4;   # E1/Ub binding
  k63 = 1.0E-3;   # Ub transfer E1->E2
  k64 = 1.0E-3;   # Monoubiquitination
  k65 = 1.0E-2;   # Polyubiquitination
  k66 = 1.0E-5;   # De-ubiquitination
  k67 = 1.0E-5;   # Proteasome binding
  k68 = 1.0E-5;   # De-ubiquitination of bound MisP
  k69 = 1.0E-3;   # Proteasome Activity
  k71 = 1.0E-8;   # Aggregation from MisP
  k72 = 1.0E-8;   # Aggregation from ubiquitinated MisP
  k73 = 1.0E-3;   # Sequestering of Aggregates
  k74 = 1.0E-5;   # Proteasome Inhibition by AggP (typo in table, should be mol-1 s-1)

  //// Other declarations:
  var k62;
  const cell, ROS, ATP, ADP, AMP;
  const k1, k2, k3, k61, k61r, k63, k64, k65, k66, k67, k68, k69;
  const k71, k72, k73, k74;

  //// Display Names:
  NatP is "Native protein";
  MisP is "Misfolded protein";
  Ub is "Ubiquitin";
  E1 is "Ubiquitin activating enzyme";
  E2 is "Ubiquitin conjugating enzyme";
  E3 is "Ubiquitin ligase (HECT)";
  DUB is "De-ubiquitinating enzyme";
  Proteasome is "Proteasome";
  ROS is "Reactive oxygen species";
  ATP is "Adenosine triphosphate";
  ADP is "Adenosine diphosphate";
  AMP is "Adenosine monophosphate";
  E3_MisP is "Misfolded protein bound by E3";
  E1_Ub is "E1 bound by Ub";
  E2_Ub is "E2 bound by Ub";
  MisP_Ub is "Monoubiquitinated misfolded protein";
  MisP_Ub2 is "Polyubiquitinated misfolded protein (2 Ub)";
  MisP_Ub3 is "Polyubiquitinated misfolded protein (3 Ub)";
  MisP_Ub4 is "Polyubiquitinated misfolded protein (4 Ub)";
  MisP_Ub5 is "Polyubiquitinated misfolded protein (5 Ub)";
  MisP_Ub6 is "Polyubiquitinated misfolded protein (6 Ub)";
  MisP_Ub7 is "Polyubiquitinated misfolded protein (7 Ub)";
  MisP_Ub8 is "Polyubiquitinated misfolded protein (8 Ub)";
  MisP_Ub4_Proteasome is "Polyubiquitinated protein (4 Ub) bound to proteasome";
  MisP_Ub5_Proteasome is "Polyubiquitinated protein (5 Ub) bound to proteasome";
  MisP_Ub6_Proteasome is "Polyubiquitinated protein (6 Ub) bound to proteasome";
  MisP_Ub7_Proteasome is "Polyubiquitinated protein (7 Ub) bound to proteasome";
  MisP_Ub8_Proteasome is "Polyubiquitinated protein (8 Ub) bound to proteasome";
  AggP is "Aggregated protein";
  SeqAggP is "Sequestered aggregated protein";
  AggP_Proteasome is "Aggregated protein bound to proteasome";
end
